pkgbase='gettext'
pkgname=('gettext-doc' 'libintl' 'gettext-asprintf' 'gettext-dev' 'libgettext' 'gettext')
pkgver=0.19.8.1.r93.g7b9671919
pkgrel=1
pkgdesc='GNU locale utilities'
url='https://www.gnu.org/software/gettext/gettext.html'
arch=('x86_64')
license=('GPL')
depends_doc=()
depends_dev=('gettext')
depends_lib=('musl' 'libintl')
depends=('musl' 'libgettext' 'libintl' 'libxml2')
makedepends=('perl' 'ncurses' 'libxml2' 'libunistring' 'coreutils')
source=('git://git.sv.gnu.org/gettext.git'
        'git://git.sv.gnu.org/gnulib.git'
        'perl.patch'
        'busybox.patch')
sha256sums=('SKIP'
            'SKIP'
            'c9ddcfa504293e595a06804583a9cdbf2c00c9c08ca763946bea8de948e211f1'
            '2c11a355770abff642643c141588a2d76e046fbb9a8f85343d15e2757d91e340')

pkgver() {
  cd "${pkgbase}"
  
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${pkgbase}"
  
  patch -Np1 -i "${srcdir}/perl.patch"
  patch -Np1 -i "${srcdir}/busybox.patch"
  
  rm "${srcdir}/gettext/gnulib-local/lib/unistd.in.h.diff"
  rm "${srcdir}/gettext/gnulib-local/lib/regexec.c.diff"
}

build() {
  cd "${pkgbase}"
  
  CFLAGS=${CFLAGS//'-flto=thin'/'-flto'}
  CXXFLAGS=${CXXFLAGS//'-flto=thin'/'-flto'}
  LDFLAGS=${LDFLAGS//'-flto=thin'/'-flto'}
  
  GNULIB_SRCDIR="${srcdir}/gnulib" ./autogen.sh --no-git
  
  ./configure --prefix='/usr' \
              --enable-threads='posix' \
              --disable-java \
              --disable-static \
              --without-xz

  make
}

package() {
  package-init
  
  cd "${pkgbase}"
  
  make DESTDIR="${tmpdir}" install

  rm "${tmpdir}/usr/include/libintl.h"
  
  rm "${tmpdir}/usr/lib/charset.alias"
}

package_gettext-doc() {
  package-doc
}

package_libintl() {
  pkgdesc="GNU gettext runtime library"
  
  install -d "$pkgdir/usr/lib"
  mv "$tmpdir"/usr/lib/libintl.so.* "$pkgdir/usr/lib/"
  
  chmod +x "$pkgdir"/usr/lib/libintl.so.*
}

package_gettext-asprintf() {
  pkgdesc="GNU gettext asprintf library"
  
  install -d "${pkgdir}/usr/lib"
  mv "${tmpdir}"/usr/lib/libasprintf.so.* "${pkgdir}/usr/lib/"
}

package_gettext-dev() {
  package-dev
}

package_libgettext() {
  package-lib
}

package_gettext() {
  package-base
}
